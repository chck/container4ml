variable "container4ml" {
  type = map(string)
  default = {
    domain  = "_YOUR_DOMAIN"
    lb_name = "container4ml-lb"
  }
}

resource "google_cloud_run_service" "container4ml" {
  name     = "container4ml"
  location = var.common.region
  depends_on = [
    google_vpc_access_connector.redis_vpc,
    google_redis_instance.redis,
  ]
  metadata {
    annotations = {
      "run.googleapis.com/vpc-access-connector" : google_vpc_access_connector.redis_vpc.id
    }
  }
  template {
    spec {
      containers {
        image = "gcr.io/cloudrun/hello"
        env {
          name  = "REDISHOST"
          value = google_redis_instance.redis.host
        }
      }
    }
  }

  traffic {
    percent         = 100
    latest_revision = true
  }
}

resource "google_compute_region_network_endpoint_group" "container4ml" {
  provider              = google
  name                  = "container4ml-neg"
  network_endpoint_type = "SERVERLESS"
  region                = var.common.region
  cloud_run {
    service = google_cloud_run_service.container4ml.name
  }
}

module "lb_http" {
  source  = "GoogleCloudPlatform/lb-http/google//modules/serverless_negs"
  version = "~> 6.1.0"

  project = var.common.project
  name    = var.container4ml.lb_name

  ssl                             = true
  managed_ssl_certificate_domains = [var.container4ml.domain]
  https_redirect                  = true

  backends = {
    default = {
      description = null
      groups = [
        {
          group = google_compute_region_network_endpoint_group.container4ml.id
        }
      ]
      enable_cdn              = false
      security_policy         = null
      custom_request_headers  = null
      custom_response_headers = null

      iap_config = {
        enable               = false
        oauth2_client_id     = null
        oauth2_client_secret = null
      }
      log_config = {
        enable      = false
        sample_rate = null
      }
    }
  }
}

data "google_iam_policy" "noauth" {
  binding {
    role = "roles/run.invoker"
    members = [
      "allUsers",
    ]
  }
}

resource "google_cloud_run_service_iam_policy" "noauth" {
  location = google_cloud_run_service.container4ml.location
  project  = google_cloud_run_service.container4ml.project
  service  = google_cloud_run_service.container4ml.name

  policy_data = data.google_iam_policy.noauth.policy_data
}

output "load-balancer-ip" {
  value = module.lb_http.external_ip
}
